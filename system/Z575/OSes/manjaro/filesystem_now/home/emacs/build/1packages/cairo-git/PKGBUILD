# Contributor: dx <dx@dxzone.com.ar>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=cairo-git
pkgver=1.14.0.14.gadbeb3d
pkgrel=1
pkgdesc="Cairo vector graphics library (git version)"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa' 'libgl' 'lzo')
makedepends=('mesa-libgl' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc' 'valgrind' 'git')
provides=("cairo=$pkgver" "cairo-xcb=$pkgver")
conflicts=('cairo' 'cairo-xcb')
source=('git://anongit.freedesktop.org/git/cairo')
sha1sums=('SKIP')

pkgver() {
	cd cairo
	git describe --always | sed 's|-|.|g'
}

build() {
	cd cairo
	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-lto \
		--enable-tee \
		--enable-gl \
		--enable-egl \
		--enable-svg \
		--enable-ps \
		--enable-pdf \
		--enable-gobject \
		--enable-gtk-doc
  make
}

package(){
	cd cairo
	make DESTDIR="${pkgdir}" install
}
