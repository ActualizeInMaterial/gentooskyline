# Maintainer : Dave Reisner <d@falconindy.com>

pkgname=pyalpm-git
pkgver=0.5.3.14.g5e88d29
pkgrel=1
pkgdesc="Libalpm bindings for Python 3"
arch=('i686' 'x86_64')
url="http://projects.archlinux.org/users/remy/pyalpm.git/"
license=('GPL')
depends=('python>=3.2' 'pacman-git')
provides=('pyalpm')
conflicts=('pyalpm')
makedepends=('git')

_gitname="pyalpm"
source=("${_gitname}::git://projects.archlinux.org/users/remy/pyalpm.git")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  git describe --always | sed -e 's|-|.|g'
}

build() {
  cd "${_gitname}"
  # workaround Python packaging issue (FS#34658)
  CFLAGS+=" -Wno-format"
  python setup.py build
}

package() {
  cd "${_gitname}"
  python setup.py install --root="${pkgdir}"
}

