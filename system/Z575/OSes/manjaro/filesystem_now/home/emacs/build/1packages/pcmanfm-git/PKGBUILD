# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=pcmanfm-git
pkgver=1.2.3.r36.gde9129c
pkgrel=1
pkgdesc="An extremely fast, lightweight, yet feature-rich file manager with tabbed browsing"
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')

#this must be installed by yaourt (in manjaro) instead of makepkg -s or else says not found
depends=('libfm-gtk3-git')

makedepends=('git' 'intltool' 'pkg-config')
optdepends=('gvfs: mounting of local and remote drives'
			'lxmenu-data: applications menu and "Open with" dialog'
			'gnome-menus: applications menu and "Open with" dialog')
provides=('pcmanfm')
conflicts=('pcmanfm')
install=pcmanfm.install
source=('git://git.lxde.org/git/lxde/pcmanfm' 'pcmanfm.install')
md5sums=('SKIP'
         'fc9ea2af71f55f32c8b22bf5ea879e69')

pkgver() {
	cd pcmanfm
	git describe --always --long | sed 's/-/-r/; s/-/./g'
}

prepare(){
	cd pcmanfm
# git checkout 27c8e63
}

build() {
	cd pcmanfm
	./autogen.sh
	./configure --prefix=/usr --sysconfdir=/etc --with-gtk=3
	make LDFLAGS="-lm ${LDFLAGS}"
}

package() {
	make -C pcmanfm DESTDIR="$pkgdir" install
}
