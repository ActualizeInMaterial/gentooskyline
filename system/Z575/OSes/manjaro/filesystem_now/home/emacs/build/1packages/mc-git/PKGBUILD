# Maintainer: Chirantan Ekbote <chirantan.ekbote@gmail.com>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Tsekhovoy Eugene aka Krash <8552246@gmail.com>

#get it ready for gdb debugging
DEBUG=0

pkgname=mc-git
pkgver=4.8.14.3.g7c39038
pkgrel=1
pkgdesc='GNU Midnight Commander is an s-lang based file manager'
url='https://midnight-commander.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'slang' 'perl')
makedepends=('git')
conflicts=('mc' 'mc-cvs' 'mc-fork-svn' 'mc-mp' 'mc-slavz' 'mc-suse')
provides=('mc')
changelog=CHANGELOG
fixflushpatch="fix_the_sometimes_truncated_shell_output_in_mc.patch"
source=('git+https://github.com/MidnightCommander/mc'
"$fixflushpatch"
)
if [ -n "$DEBUG" ]; then
  options+=(!strip)
fi
sha512sums=('SKIP'
            'bc17849a6f7ee577be44282df01df744f0ab15b5b3b47478f4634bdd17120b24f23664662050f0857c2315845aaa137e9f916e166a0140c6262192ef8e534b9c')

pkgver() {
    cd mc
    git describe | sed 's/-/./g'
}

prepare() { #this is temporarily here, but should be in build() because of the git updates and --incremental parameter to makepkg requiring this to be reexecuted just in case the code changes are in fact requiring rerun of autogen/configure
    cd mc
    # http://git.yoctoproject.org/cgit.cgi/poky-contrib/commit/?h=dcui/mc&id=338da10abcd744ab75dfb184f8f81370727e5653
    sed -i 's/pkglibexec_SCRIPTS = $(SCRIPTS_OUT)/pkgdata_SCRIPTS = $(SCRIPTS_OUT)/g' contrib/Makefile.am
    patch -Np1 -i "../${fixflushpatch}"
}

build() {
    #debug mode for gdb, in case seg faults
    if [ -n "$DEBUG" ]; then
      export CFLAGS="$CFLAGS -O0 -fbuiltin -g"
      export CXXFLAGS="$CXXFLAGS -O0 -fbuiltin -g"
    fi

    cd mc

    ./autogen.sh
    export MCREVISION=$(git describe --tags)
    ./configure                     \
	--prefix=/usr               \
	--sysconfdir=/etc           \
	--libexecdir=/usr/lib       \
	--enable-background         \
	--enable-network            \
	--enable-netcode            \
	--enable-charset            \
	--enable-nls                \
	--with-vfs                  \
	--with-edit                 \
	--with-screen=slang         \
	--without-x                 \
	--without-samba             \
	--without-gpm-mouse         \
	--without-gnome             \
	--without-debug             \
	--without-included-gettext  \
	--disable-dependency-tracking


    echo ${MAKEFLAGS}
    make ${MAKEFLAGS}
}

package() {
    cd mc
    make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 ft=sh et:
