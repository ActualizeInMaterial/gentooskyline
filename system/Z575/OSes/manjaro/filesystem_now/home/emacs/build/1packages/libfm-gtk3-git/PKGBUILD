# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributor: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_gitname=libfm
pkgname=libfm-gtk3-git
pkgver=20150613
pkgrel=1
pkgdesc="A glib/gio-based library to develop file managers. Include GTK3 components."
arch=('i686' 'x86_64')
url="http://www.lxde.org"
license=('GPL2')
depends=('libexif' 'desktop-file-utils' 'gtk3' 'menu-cache')
makedepends=('git' 'vala' 'gtk-doc' 'intltool')  
provides=('libfm')
conflicts=('libfm')
source=("git+https://github.com/lxde/libfm.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
	cd "$srcdir/$_gitname"
	./autogen.sh
	./configure --with-gtk=3 --enable-gtk-doc-html=no --enable-actions --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
