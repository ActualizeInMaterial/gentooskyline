#Maintainer : Grigory Mishchenko <grishkokot[at]gmail[dot]com>

pkgname=menu-cache-git
pkgver=0.5.1.11.gae8bfbd
pkgrel=1
pkgdesc='Caching mechanism for freedesktop.org compliant menus'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
depends=('intltool' 'glib2')
options=('!libtool')
makedepends=('gtk-doc' 'git' 'intltool' 'pkg-config' 'automake' 'pkgconfig')
conflicts=('menu-cache')
provides=('menu-cache')

source=('git://lxde.git.sourceforge.net/gitroot/lxde/menu-cache')
sha512sums=('SKIP')

_gitname="menu-cache"

pkgver() {
    cd $_gitname
    git describe --always | sed 's|-|.|g'
}

build() {
    cd $_gitname

    gtkdocize
    autoreconf -ivf

    ./configure --prefix=/usr --sysconfdir=/etc \
                --libexecdir=/usr/lib/menu-cache \
                --disable-static
    make
}

package() {
    cd "${_gitname}"
    make DESTDIR="$pkgdir" install
}
